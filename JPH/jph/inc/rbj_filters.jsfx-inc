//author: witti

@init

function rbjslider() (
// frequency slider scaling
tmpx = 16+slider33*1.20103;
tmpy = floor(exp(tmpx*log(1.059))*8.17742);

// filter calculation
f = 2*$pi * min(tmpy, 0.49 * srate) / srate;
q =max(min(slider34,4),0.01);
dB_gain = slider35;

slider36==0?(
//LPF:
w0 = f;
cos_w0 = cos(w0);
alpha = sin(w0) / (2*q);

b1 = 1 - cos_w0;
b0 = b2 = 0.5 * b1;
a0 = 1 + alpha;
a1 = -2 * cos_w0;
a2 = 1 - alpha;
);

slider36==1?(
//HPF:
w0 = f;
cos_w0 = cos(w0);
alpha = sin(w0) / (2*q);

b1 = -1 - cos_w0;
b0 = b2 = -0.5 * b1;
a0 = 1 + alpha;
a1 = -2 * cos_w0;
a2 = 1 - alpha;
);

slider36==2?(
//BPF:
w0 = f;
alpha = sin(w0) / (2*q);

b0 = q * alpha;
b1 = 0;
b2 = -b0;
a0 = 1 + alpha;
a1 = -2 * cos(w0);
a2 = 1 - alpha;
);

slider36==3?(
//BPF2:
w0 = f;
alpha = sin(w0) / (2*q);

b0 = alpha;
b1 = 0;
b2 = -alpha;
a0 = 1 + alpha;
a1 = -2 * cos(w0);
a2 = 1 - alpha;
);

slider36==4?(
//NOTCH:
w0 = f;
alpha = sin(w0) / (2*q);

b0 = b2 = 1;
b1 = a1 = -2 * cos(w0);
a0 = 1 + alpha;
a2 = 1 - alpha;
);

slider36==5?(
//ALL-PASS:
w0 = f;
alpha = sin(w0) / (2*q);

b0 = a2 = 1 - alpha;
b1 = a1 = -2 * cos(w0);
b2 = a0 = 1 + alpha;
);

slider36==6?(
//PEQ:
w0 = f;
alpha = sin(w0) / (2*q);
a = 10^(db_gain / 40);

b0 = 1 + alpha * a;
b1 = a1 = -2 * cos(w0);
b2 = 1 - alpha * a;
a0 = 1 + alpha / a;
a2 = 1 - alpha / a;
);

slider36==7?(
//LSH:
w0 = f;
cos_w0 = cos(w0);
a = 10^(db_gain / 40);

tmp0 = 2 * sqrt(a) * sin(w0) / (2 * q);
tmp1 = (a + 1) - (a - 1) * cos_w0;
tmp2 = (a + 1) + (a - 1) * cos_w0;

b0 = a * (tmp1 + tmp0);
b1 = 2 * a * ((a - 1) - (a + 1) * cos_w0);
b2 = a * (tmp1 - tmp0);
a0 = tmp2 + tmp0;
a1 = -2 * ((a - 1) + (a + 1) * cos_w0);
a2 = tmp2 - tmp0;
);

slider36==8?(
//HSH:
w0 = f;
cos_w0 = cos(w0);
a = 10^(db_gain / 40);

tmp0 = 2 * sqrt(a) * sin(w0) / (2 * q);
tmp1 = (a + 1) - (a - 1) * cos_w0;
tmp2 = (a + 1) + (a - 1) * cos_w0;

b0 = a * (tmp2 + tmp0);
b1 = -2 * a * ((a - 1) + (a + 1) * cos_w0);
b2 = a * (tmp2 - tmp0);
a0 = tmp1 + tmp0;
a1 = 2 * ((a - 1) - (a + 1) * cos_w0);
a2 = tmp1 - tmp0;
);

a1 /= a0;
a2 /= a0;
b0 /= a0;
b1 /= a0;
b2 /= a0;
);

function rbjsample() (
inA = spl0;
inB = spl1;

//Left
x2A = x1A;
x1A = x0A;
x0A = inA;

y2A = y1A;
y1A = y0A;
y0A = b0*x0A + b1*x1A + b2*x2A - a1*y1A - a2*y2A;

//Right
x2B = x1B;
x1B = x0B;
x0B = inB;

y2B = y1B;
y1B = y0B;
y0B = b0*x0B + b1*x1B + b2*x2B - a1*y1B - a2*y2B;

//Output
spl0 = y0A;
spl1 = y0B;

spl0 *= outgain;
spl1 *= outgain;
);

function draw_frequency_grid() local(fi, fmag, freq, xratio) (
	ui_push();
		ui_retina(1);
		fi = 1;
		fmag = 10;
		while (fi*fmag < high_freq) (
			freq = fi*fmag;
			freq > low_freq ? (
				xratio = log(freq/low_freq)/log(high_freq/low_freq);
				gfx_x = ui_left() + xratio*ui_width();
				gfx_y = ui_top();
				ui_color(255, 255, 255, 0.3);
				gfx_lineto(gfx_x, ui_bottom());

				fi == 1 ? (
					ui_fontsize(10*gfx_ext_retina);
					ui_color(255, 255, 255, 0.5);
					gfx_x += ui_texth()*0.5;
					gfx_y = ui_bottom() - ui_texth()*1.5;
					fmag >= 1000 ? (
						gfx_printf("%ikHz", freq/1000);
					) : (
						gfx_printf("%iHz", freq);
					);
				);
			);
			fi += 1;
			fi >= 10 ? (
				fi /= 10;
				fmag *= 10;
			);
		);
	ui_pop();
);




function rbjgfx() (
            ui_split_topratio(1/10);
            ui_automate(slider36, multipleway_switch(slider36, "lp", 0, 20, 12));
            ui_split_next();
                ui_automate(slider36, multipleway_switch(slider36, "hp", 1, 20, 12));
            ui_split_next();
                ui_automate(slider36, multipleway_switch(slider36, "bp", 2, 20, 12));
            ui_split_next();
                ui_automate(slider36, multipleway_switch(slider36, "bp2", 3, 20, 12));
            ui_split_next();
                ui_automate(slider36, multipleway_switch(slider36, "notch", 4, 20, 12));
            ui_split_next();
                ui_automate(slider36, multipleway_switch(slider36, "allpass", 5, 20, 12));
            ui_split_next();
                ui_automate(slider36, multipleway_switch(slider36, "peak", 6, 20, 12));
            ui_split_next();
                ui_automate(slider36, multipleway_switch(slider36, "lsf", 7, 20, 12));
            ui_split_next();
                ui_automate(slider36, multipleway_switch(slider36, "hsf", 8, 20, 12));

            ui_pop();
        ui_pop();
                
        slider36 == 0 ? ( //lp
                ui_pop();
        ui_pop();
                
                //WINDOW
        ui_split_leftratio(0.6);
            ui_split_topratio(1/2);
            draw_lp(slider33, 12, slider34, 0, 100, 0, 4);
                
            ui_split_next();
            ui_split_leftratio(1/2);
                    ui_automate(slider33, labelled_dial(slider33, 0, 0, 100, 0, "cutoff", floor(exp((16+slider33*1.20103)*log(1.059))*8.17742), "%i Hz", 60,60,12)); 
            ui_split_next();
                    ui_automate(slider34, labelled_dial(slider34, 0.707, 0, 4, 0, "bandwith", slider34 , "%.1f", 60,60,12)); 
            ui_pop();
          
          ) : slider36 == 1 ? (//hp
            ui_pop();
        ui_pop();
                
                //WINDOW
        ui_split_leftratio(0.6);
            ui_split_topratio(1/2);
            draw_hp(slider33, 12, slider34, 0, 100, 0, 4);

            ui_split_next();
            ui_split_leftratio(1/2);
                    ui_automate(slider33, labelled_dial(slider33, 0, 0, 100, 0, "cutoff", floor(exp((16+slider33*1.20103)*log(1.059))*8.17742), "%i Hz", 60,60,12)); 
            ui_split_next();
                    ui_automate(slider34, labelled_dial(slider34, 0.707, 0, 4, 0, "bandwith", slider34 , "%.1f", 60,60,12)); 
            ui_pop();
            
          ) : slider36 == 2 ? (//bp
            ui_pop();
        ui_pop();
                
                //WINDOW
        ui_split_leftratio(0.6);
            ui_split_topratio(1/2);
                draw_bp(slider33, 12, slider34, 0, 100, 0, 4);
                
            ui_split_next();
            ui_split_leftratio(1/2);
                    ui_automate(slider33, labelled_dial(slider33, 0, 0, 100, 0, "cutoff", floor(exp((16+slider33*1.20103)*log(1.059))*8.17742), "%i Hz", 60,60,12)); 
            ui_split_next();
                    ui_automate(slider34, labelled_dial(slider34, 0.707, 0, 4, 0, "bandwith", slider34 , "%.1f", 60,60,12)); 
            ui_pop();
            
          ) : slider36 == 3 ? (//bp
            ui_pop();
        ui_pop();
                
                //WINDOW
        ui_split_leftratio(0.6);
            ui_split_topratio(1/2);
                draw_bp2(slider33, 12, slider34, 0, 100, 0, 4);
                
            ui_split_next();
            ui_split_leftratio(1/2);
                    ui_automate(slider33, labelled_dial(slider33, 0, 0, 100, 0, "cutoff", floor(exp((16+slider33*1.20103)*log(1.059))*8.17742), "%i Hz", 60,60,12)); 
            ui_split_next();
                    ui_automate(slider34, labelled_dial(slider34, 0.707, 0, 4, 0, "bandwith", slider34 , "%.1f", 60,60,12)); 
            ui_pop();
            
        ) : slider36 == 4 ? (//notch
            ui_pop();
        ui_pop();
                
                //WINDOW
        ui_split_leftratio(0.6);
            ui_split_topratio(1/2);
                draw_notch2(slider33, 12, slider34, 0, 100, 0, 4);
                
            ui_split_next();
            ui_split_leftratio(1/2);
                    ui_automate(slider33, labelled_dial(slider33, 0, 0, 100, 0, "cutoff", floor(exp((16+slider33*1.20103)*log(1.059))*8.17742), "%i Hz", 60,60,12)); 
            ui_split_next();
                    ui_automate(slider34, labelled_dial(slider34, 0.707, 0, 4, 0, "bandwith", slider34 , "%.1f", 60,60,12)); 
            ui_pop();
            
        ) : slider36 == 5 ? (//allpass
            ui_pop();
        ui_pop();
                
                //WINDOW
        ui_split_leftratio(0.6);
            ui_split_topratio(1/2);
                draw_allpass(slider33, 12, slider34, 0, 100, 0, 4);
                
            ui_split_next();
            ui_split_leftratio(1/2);
                    ui_automate(slider33, labelled_dial(slider33, 0, 0, 100, 0, "cutoff", floor(exp((16+slider33*1.20103)*log(1.059))*8.17742), "%i Hz", 60,60,12)); 
            ui_split_next();
                    ui_automate(slider34, labelled_dial(slider34, 0.707, 0, 4, 0, "bandwith", slider34 , "%.1f", 60,60,12)); 
            ui_pop();
            
        ) : slider36 == 6 ? (//peak
            ui_pop();
        ui_pop();
                
                //WINDOW
        ui_split_leftratio(0.6);
            ui_split_topratio(1/2);
                draw_peak(slider33, slider35, slider34, 0, 100, 0, 12, 0, 4);
                
            ui_split_next();
            ui_split_leftratio(1/3);
                    ui_automate(slider33, labelled_dial(slider33, 0, 0, 100, 0, "cutoff", floor(exp((16+slider33*1.20103)*log(1.059))*8.17742), "%i Hz", 60,60,12)); 
            ui_split_next();
                    ui_automate(slider34, labelled_dial(slider34, 0.707, 0, 4, 0, "bandwith", slider34 , "%.1f", 60,60,12)); 
            ui_split_next();
                    ui_automate(slider35, labelled_dial(slider35, 0, -12, 12, 0, "db", slider35 , "%.1f dB", 60,60,12)); 
            ui_pop();
            
        ) : slider36 == 7 ? (//lsf
            ui_pop();
        ui_pop();
                
                //WINDOW
        ui_split_leftratio(0.6);
            ui_split_topratio(1/2);
                draw_lsf(slider33, slider35, slider34, 0, 100, 0, 12, 0, 4);
                
            ui_split_next();
            ui_split_leftratio(1/3);
                    ui_automate(slider33, labelled_dial(slider33, 0, 0, 100, 0, "cutoff", floor(exp((16+slider33*1.20103)*log(1.059))*8.17742), "%i Hz", 60,60,12)); 
            ui_split_next();
                    ui_automate(slider34, labelled_dial(slider34, 0.707, 0, 4, 0, "bandwith", slider34 , "%.1f", 60,60,12)); 
            ui_split_next();
                    ui_automate(slider35, labelled_dial(slider35, 0, -12, 12, 0, "db", slider35 , "%.1f dB", 60,60,12)); 
            ui_pop();
            
        ) : slider36 == 8 ? (//hsf
            ui_pop();
        ui_pop();
                
                //WINDOW
        ui_split_leftratio(0.6);
            ui_split_topratio(1/2);
                draw_hsf(slider33, slider35, slider34, 0, 100, 0, 12, 0, 4);
                
            ui_split_next();
            ui_split_leftratio(1/3);
                    ui_automate(slider33, labelled_dial(slider33, 0, 0, 100, 0, "cutoff", floor(exp((16+slider33*1.20103)*log(1.059))*8.17742), "%i Hz", 60,60,12)); 
            ui_split_next();
                    ui_automate(slider34, labelled_dial(slider34, 0.707, 0, 4, 0, "bandwith", slider34 , "%.1f", 60,60,12)); 
            ui_split_next();
                    ui_automate(slider35, labelled_dial(slider35, 0, -12, 12, 0, "db", slider35 , "%.1f dB", 60,60,12)); 
            ui_pop();
            
        );
);

//witti WAVESHAPER

@init

function wittislider() (
gain = (slider21/10)+1;
drive = max(min(slider22,99),0);
a = sin(((drive+1)/101)*($pi/2));
k = 2*a/(1-a);

f = 2*$pi * min(slider23, 0.49 * srate) / srate;
q = 0.707;

w0 = f;
cos_w0 = cos(w0);
alpha = sin(w0) / (2*q);

b1 = 1 - cos_w0;
b0 = b2 = 0.5 * b1;
a0 = 1 + alpha;
a1 = -2 * cos_w0;
a2 = 1 - alpha;

a1 /= a0;
a2 /= a0;
b0 /= a0;
b1 /= a0;
b2 /= a0;
);

function wittisample() (
spl0*=gain;
spl1*=gain;

spl0 = (1+k)*(spl0)/(1+k*abs(spl0));
spl1 = (1+k)*(spl1)/(1+k*abs(spl1));

inA = spl0;
inB = spl1;

//Left
x2A = x1A;
x1A = x0A;
x0A = inA;

y2A = y1A;
y1A = y0A;
y0A = b0*x0A + b1*x1A + b2*x2A - a1*y1A - a2*y2A;

//Right
x2B = x1B;
x1B = x0B;
x0B = inB;

y2B = y1B;
y1B = y0B;
y0B = b0*x0B + b1*x1B + b2*x2B - a1*y1B - a2*y2B;

//Output
spl0 = y0A;
spl1 = y0B;
);

function wittigfx() (
    ui_split_leftratio(0.6);
        ui_split_leftratio(1/3);
            ui_automate(slider21, labelled_dial(slider21, 0, 0, 100, 0, "boost", slider21, "%.1f %%", 50,50,12));
        ui_split_next();
            ui_automate(slider22, labelled_dial(slider22, 0, 0, 100, 0, "drive", slider22, "%.1f %%", 50,50,12));
        ui_split_next();
            ui_automate(slider23, labelled_dial(slider23, 12000, 1000, 12000, 0, "lp", slider23, "%.i Hz", 50,50,12));
        ui_pop();
    ui_pop();
    
        draw_wavescope();  
);
